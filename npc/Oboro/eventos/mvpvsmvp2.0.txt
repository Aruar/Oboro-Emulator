/*
-- ----------------------------
-- Table structure for mvpvsmvp
-- ----------------------------
DROP TABLE IF EXISTS `mvpvsmvp`;
CREATE TABLE `mvpvsmvp` (
  `account_id` int(11) DEFAULT NULL,
  `opcion` tinyint(4) DEFAULT NULL
) ENGINE=InnoDB DEFAULT CHARSET=utf8;
*/

-	script	mvpvsmvp	-1,{
OnWhisperGlobal:
	if ( getgmlevel() >= 60 ) 
	{
		if (@whispervar0$ == "activar")
			donpcevent "mvpvsmvp::OnWatingRoom";
	}
end;

OnInit:
	set $@termino,0;
	set $@Reload, 0;
end;
	
OnEventReload:
	set $@Reload, $@Reload + 1;
	if ($@Reload == 3)
	{
		set $@Reload, 0;
		set $@termino, 0;
		announce " [MvP vs MvP]: El evento ha terminado! gracias por participar.",bc_all;
		end;
	}
OnClock1300:
OnClock1600:
OnClock1900:
OnClock2100:
OnWatingRoom:
	query_sql "DELETE FROM `mvpvsmvp`";
	set $@termino, 1;
	
	killmonsterall "g_room1-2",2;
	setarray $@MobID[0],1511,1647,1785,1039,1874,2068,1272,1719,1046,1871,1768,1832,1734,2022,1087,1190,1623,1708,1751,1917,1832,2022,1646,1650,1708;
	do 
	{
		set $@Rand1,(rand(0,getarraysize($@MobID)));
		set $@Rand2,(rand(0,getarraysize($@MobID)));
	
		set $@MName1$, getmonsterinfo($@MobID[$@Rand1], MOB_NAME );
		set $@HpMob1 , getmonsterinfo($@MobID[$@Rand1], MOB_MAXHP);
		set $@MobLv1 , getmonsterinfo($@MobID[$@Rand1], MOB_LV);
		set $@MName2$, getmonsterinfo($@MobID[$@Rand2], MOB_NAME );
		set $@HpMob2 , getmonsterinfo($@MobID[$@Rand2], MOB_MAXHP);
		set $@MobLv2 , getmonsterinfo($@MobID[$@Rand1], MOB_LV);
		
		if (
			($@MobLv1 > $@MobLv2 && ($@MobLv1 - $@MobLv2) > 15) ||
			($@MobLv2 > $@MobLv1 && ($@MobLv2 - $@MobLv1) > 15)
		)
			set $@error, 1;
		else
			set $@error, 0;

	} while ( 
			$@MName1$ == "null" ||
			$@MName2$ == "null" || 
			$@HpMob1 == 0 ||
			$@HpMob2 == 0 || 
			$@MobID[$@Rand1] == 0 ||
			$@MobID[$@Rand2] == 0 || 
			$@MobID[$@Rand1] == $@MobID[$@Rand2] ||
			$@error == 1			
	);
	
	if ( $@Reload == 0 )
		announce " [MvP vs MvP]: El evento comenzará pronto! Warp en Morocc ["+$@MName1$+"] vs ["+$@MName2$+"] por 10 Event Coins.",bc_all;
	else
		announce " [MvP vs MvP]: El Round "+$@Reload+" está por comenzar ["+$@MName1$+"] vs ["+$@MName2$+"] por 10 Event Coins.",bc_all;

	sleep 14000;
	
	announce "[MvP vs MvP]: En 1 minuto se deshabilitaran las apuestas...",bc_all;
	sleep 60000;
	//sleep 10000;
	donpcevent "mvpvsmvp::OnStart";
end;


OnStart:
	announce "[MvP vs MvP]: Se cerraron las puertas.",bc_all;
	set $@termino , 2;
	// mobevent <map>,  <x>,<y>,<fakename>,        <mobid>,<size>,<amount>,<teamid>,<showhp>,<increasehp>,<allied>,<noslaves>,<noexpnodrop>,<poringcoins>,<event>
	mobevent "g_room1-2",44,369,$@MName1$,$@MobID[$@Rand1],rand(0,2),1,1,0,500000,1,0,0,0,0,0,0,0,"mvpvsmvp::OnEquip2Win";
	mobevent "g_room1-2",40,369,$@MName2$,$@MobID[$@Rand2],rand(0,2),1,0,0,500000,0,0,0,0,0,0,0,0,"mvpvsmvp::OnEquip1Win";
end;


OnEquip2Win: 
	announce "[MvP vs MvP]: "+$@MName2$+" Ganó, tienen 1 minuto para retirar su premio en el warp del evento,",bc_all; 
	query_sql "SELECT `account_id` FROM `mvpvsmvp` WHERE `opcion`=2",.@aid;
	for (set .@i, 0; .@i < getarraysize(.@aid); set .@i, .@i + 1)
	{
		detachrid; 
		attachrid(.@aid[.@i]);
		getitem 15803, 10, .@aid[.@i];
		message rid2name(.@aid[.@i]),"Felicidades por ganar el evento MVP vs MVP!!.";
	}
	mapwarp "g_room1-2","morocc",158,90;
	donpcevent "mvpvsmvp::OnEventReload";
end;

OnEquip1Win:
	announce "[MvP vs MvP]: "+$@MName1$+" Ganó, Felicidades a los ganadores",bc_all; 
	query_sql "SELECT `account_id` FROM `mvpvsmvp` WHERE `opcion`=1",.@aid;
	for (set .@i, 0; .@i < getarraysize(.@aid); set .@i, .@i + 1)
	{
		detachrid; 
		attachrid(.@aid[.@i]);
		getitem 15803, 10, .@aid[.@i];
		message rid2name(.@aid[.@i]),"Felicidades por ganar el evento MVP vs MVP!!.";
	}	
	mapwarp "g_room1-2","morocc",158,90; 
	donpcevent "mvpvsmvp::OnEventReload";
end;

}

morocc,159,84,5	script	Entrada MVP	45,2,2,{
	if ( $@termino == 1 )
	{
		mes "^10B2D2 MvP vs MvP^000000"; 
		mes " "; 

		if ( harm_map_multicount( "g_room1-2" ) > 0 ) 
			mclose "No puedes hacer doble login en este mapa";

		mes "A que mvp queres apostar";
		set .@opcion, select("^10B2D2 [ > ] ^000000 " + $@MName1$ + ":^10B2D2 [ > ] ^000000 " + $@MName2$);
		
		if ( .@opcion == 1 )
			query_sql "INSERT INTO `mvpvsmvp` VALUES("+getcharid(3)+",1)";
		else
			query_sql "INSERT INTO `mvpvsmvp` VALUES("+getcharid(3)+",2)";
		close2;
			dispbottom "Has apostado por : " + ( .@opcion == 1 ? $@MName1$ : $@MName2$); 
			warp "g_room1-2",40,380;
		end;
	}
	end;
}

g_room1-2	mapflag	monster_noteleport
g_room1-2	mapflag	noteleport
g_room1-2	mapflag	nobranch
g_room1-2	mapflag	nosave
g_room1-2	mapflag	noloot
g_room1-2	mapflag	novending
g_room1-2	mapflag	nowarp